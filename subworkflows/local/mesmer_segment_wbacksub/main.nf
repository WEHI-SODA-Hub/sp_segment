include { BACKGROUNDSUBTRACT } from '../backgroundsubtract/main.nf'
include { MESMER_SEGMENT     } from '../mesmer_segment/main.nf'

workflow MESMER_SEGMENT_WBACKSUB {

    take:
    ch_mesmer_wbacksub

    main:

    ch_versions = Channel.empty()

    //
    // Run background subtraction
    //
    BACKGROUNDSUBTRACT(
        ch_mesmer_wbacksub.map {
            sample,
            _run_backsub,
            _run_mesmer,
            _run_cellpose,
            tiff,
            _nuclear_channel,
            _membrane_channels -> [
                sample,
                tiff
            ]
        }
    )
    ch_versions = ch_versions.mix(BACKGROUNDSUBTRACT.out.versions.first())


    // Replace tiff with backsub_tif
    ch_mesmer_wbacksub
        .join( BACKGROUNDSUBTRACT.out.backsub_tif )
        .map {
            sample,
            run_backsub,
            run_mesmer,
            run_cellpose,
            _tiff,
            nuclear_channel,
            membrane_channels,
            backsub_tiff -> [
                sample,
                run_backsub,
                run_mesmer,
                run_cellpose,
                backsub_tiff,
                nuclear_channel,
                membrane_channels
            ]
        }.set { ch_mesmer }

    MESMER_SEGMENT(
        ch_mesmer
    )
    ch_versions = ch_versions.mix(MESMER_SEGMENT.out.versions.first())

    emit:
    annotations      = MESMER_SEGMENT.out.annotations       // channel: [ val(meta), *.geojson ]
    whole_cell_tif   = MESMER_SEGMENT.out.whole_cell_tif    // channel: [ val(meta), *.tiff ]
    nuclear_tif      = MESMER_SEGMENT.out.nuclear_tif       // channel: [ val(meta), *.tiff ]

    versions = ch_versions                                  // channel: [ versions.yml ]
}
